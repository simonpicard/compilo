<Program>-><Instruction> <InstructionEnd> <InstructionList> <Program>
         -><Empty> <Program>
         ->EPSILON_VALUE
<Empty>-><InstructionEnd> <EmptyTail>
<EmptyTail>-><InstructionEnd> <EmptyTail>
           ->EPSILON_VALUE
<Definition>->IDENTIFIER TYPE_DEFINITION <Expression>
            ->CONST <Assignation>
<Block>->LET <Assignation> <Empty> <InstructionList> END
<InstructionList>-><Instruction> <Empty> <InstructionList>
                 -><Empty> <InstructionList>
                 ->EPSILON_VALUE
<Instruction>-><Assignation>
             -><Loop>
             -><Definition>
             -><Block>
<InstructionEnd>->END_OF_INSTRUCTION
<Loop>-><If>
      ->WHILE <Expression> <Empty> <InstructionList> END
      ->WHILE <Expression> <Empty> <Instruction> END
      ->FOR IDENTIFIER ASSIGNATION <Expression> TERNARY_ELSE <Expression> <Empty> <InstructionList> END
      ->FOR IDENTIFIER ASSIGNATION <Expression> TERNARY_ELSE <Expression> <Empty> <Instruction> END
      ->FOR IDENTIFIER ASSIGNATION <Expression> TERNARY_ELSE <Expression> TERNARY_ELSE <Expression> <Empty> <InstructionList> END
      ->FOR IDENTIFIER ASSIGNATION <Expression> TERNARY_ELSE <Expression> TERNARY_ELSE <Expression> <Empty> <Instruction> END
<Assignation>->IDENTIFIER ASSIGNATION <Expression>
             ->IDENTIFIER COMMA <Assignation> COMMA <Expression>
<Expression>-><BinaryExpression> <Expression'>
<Expression'>->TERNARY_IF <Expression> TERNARY_ELSE <Expression> <Expression'>
             ->EPSILON_VALUE
<AtomicExpression>->IDENTIFIER
                  ->INTEGER
                  ->REAL
                  ->BOOLEAN
<UnaryExpression>->NEGATION <UnaryExpression>
                 -><UnaryBitwiseNotExpression>
<UnaryBitwiseNotExpression>->BITWISE_NOT <UnaryBitwiseNotExpression>
                           -><UnaryMinusPlusExpression>
<UnaryMinusPlusExpression>->MINUS <UnaryMinusPlusExpression>
                          ->PLUS <UnaryMinusPlusExpression>
                          -><UnaryAtomicExpression>
<UnaryAtomicExpression>-><AtomicExpression>
                       ->LEFT_PARENTHESIS <Expression> RIGHT_PARENTHESIS                       
<BinaryExpression>-><BinaryLazyOrExpression> <BinaryExpression'>
<BinaryExpression'>->LAZY_OR <BinaryLazyOrExpression> <BinaryExpression'>
                   ->EPSILON_VALUE
<BinaryLazyOrExpression>-><BinaryLazyAndExpression> <BinaryLazyOrExpression'>
<BinaryLazyOrExpression'>->LAZY_AND <BinaryLazyAndExpression> <BinaryLazyOrExpression'>
                         ->EPSILON_VALUE
<BinaryLazyAndExpression>-><BinaryNumericExpression> <BinaryLazyAndExpression'>
<BinaryLazyAndExpression'>->GREATER_THAN <BinaryNumericExpression> <BinaryLazyAndExpression'>
                          ->LESS_THAN <BinaryNumericExpression> <BinaryLazyAndExpression'>
                          ->GREATER_OR_EQUALS_THAN <BinaryNumericExpression> <BinaryLazyAndExpression'>
                          ->LESS_OR_EQUALS_THAN <BinaryNumericExpression> <BinaryLazyAndExpression'>
                          ->EQUALITY <BinaryNumericExpression> <BinaryLazyAndExpression'>
                          ->INEQUALITY <BinaryNumericExpression> <BinaryLazyAndExpression'>
                          ->EPSILON_VALUE
<BinaryNumericExpression>-><BinaryTermExpression> <BinaryNumericExpression'>
<BinaryNumericExpression'>->PLUS <BinaryTermExpression> <BinaryNumericExpression'>
                          ->MINUS <BinaryTermExpression> <BinaryNumericExpression'>
                          ->BITWISE_OR <BinaryTermExpression> <BinaryNumericExpression'>
                          ->BITWISE_XOR <BinaryTermExpression> <BinaryNumericExpression'>
                          ->EPSILON_VALUE
<BinaryTermExpression>-><BinaryShiftedExpression> <BinaryTermExpression'>
<BinaryTermExpression'>->ARITHMETIC_SHIFT_LEFT <BinaryShiftedExpression> <BinaryTermExpression'>
                       ->ARITHMETIC_SHIFT_RIGHT <BinaryShiftedExpression> <BinaryTermExpression'>
                       ->EPSILON_VALUE
<BinaryShiftedExpression>-><BinaryFactorExpression> <BinaryShiftedExpression'>
<BinaryShiftedExpression'>->TIMES <BinaryFactorExpression> <BinaryShiftedExpression'>
                          ->DIVIDE <BinaryFactorExpression> <BinaryShiftedExpression'>
                          ->REMAINDER <BinaryFactorExpression> <BinaryShiftedExpression'>
                          ->BITWISE_AND <BinaryFactorExpression> <BinaryShiftedExpression'>
                          ->INVERSE_DIVIDE <BinaryFactorExpression> <BinaryShiftedExpression'>
                          ->EPSILON_VALUE
<BinaryFactorExpression>-><UnaryExpression> <BinaryFactorExpression'>
<BinaryFactorExpression'>->POWER <UnaryExpression> <BinaryFactorExpression'>
                         ->EPSILON_VALUE
<If>->IF <Expression> <Empty> <InstructionList> <IfEnd>
    ->IF <Expression> <Empty> <Instruction> <IfEnd>
<IfEnd>->ELSE_IF <Expression> <Empty> <InstructionList> <IfEnd>
       ->ELSE_IF <Expression> <Empty> <Instruction> <IfEnd>
       ->ELSE <InstructionList> END
       ->ELSE <Instruction> END
       ->END
#Removal of left recursion (Expression and Binary<All>Expression)